ext.artifactoryRepos = [
        'release': 'libs-release-local',
        'snapshot': 'libs-snapshot-local',
]

publishing {
    publications {
        all {
            groupId = GROUP
            artifactId ARTIFACT_NAME
            version VERSION
        }

        snapshot(MavenPublication) {
            version = "${version}-SNAPSHOT"
        }

        release(MavenPublication) {
            version = version
        }
    }
}

artifactory {
    contextUrl = 'https://plangrid.jfrog.io/plangrid/'
    publish {
        repository {
            username = System.getenv('ARTIFACTORY_USER')
            password = System.getenv('ARTIFACTORY_PASSWORD')
            maven = true
        }
        defaults {
            publishArtifacts = true
        }
    }
}

tasks.register('publishSnapshot') {
    dependsOn 'generatePomFileForSnapshotPublication'
    doFirst {
        clientConfig.publisher.repoKey = artifactoryRepos.snapshot
        artifactoryPublish.publications(publishing.publications.snapshot)
    }
    finalizedBy artifactoryPublish
}

tasks.register('publishRelease') {
    dependsOn 'generatePomFileForReleasePublication'
    doFirst {
        clientConfig.publisher.repoKey = artifactoryRepos.release
        artifactoryPublish.publications(publishing.publications.release)
    }
    finalizedBy artifactoryPublish
}
